val metaRepoMap =
  mapOf(
    "sonatype" to "https://repository.sonatype.org/content/groups/forge/",
    "maven2" to "https://repo.maven.apache.org/maven2/",
    "gradle" to "https://plugins.gradle.org/m2/",
    "google" to "https://dl.google.com/dl/android/maven2/"
  )

val urlMap = mapOf<String, String>(
  metaRepoMap["maven2"]!!.removeSuffix("/") to "https://mirrors.cloud.tencent.com/nexus/repository/maven-public",
  metaRepoMap["google"]!!.removeSuffix("/") to "https://mirrors.cloud.tencent.com/nexus/repository/maven-public",
  metaRepoMap["gradle"]!!.removeSuffix("/") to "https://mirrors.cloud.tencent.com/nexus/repository/gradle-plugin"
)

val otherRepos = listOf(
  "https://repo.spring.io/milestone/",
  "https://repo.spring.io/snapshot/",
  "https://oss.sonatype.org/content/repositories/snapshots/"
)



fun RepositoryHandler.enableMirror() {
  whenObjectAdded {
    if (this is MavenArtifactRepository && !name.startsWith("backupRepo")) {
      var str = "mirror for ${javaClass.simpleName}"
      val oUrl = this.url.toString().removeSuffix("/")
      str += oUrl
      urlMap[oUrl]?.also {
        str += " -> $it"
        this.setUrl(it)
      }
    }
  }
}

fun RepositoryHandler.addNonExistsRepo(url: String, name: String? = null) {
  val rUrl = url.removeSuffix("/")
  val urlStringLists = map { (it as? MavenArtifactRepository)?.url?.toString()?.removeSuffix("/") }
  if (urlStringLists.contains(rUrl)) return
  else
    maven {
      setUrl(uri(url))
      name?.also { this.name = it }
    }
}

fun RepositoryHandler.addNonExistsMavenLocal() {
  val urlStringLists = map { (it as? MavenArtifactRepository)?.url?.toString()?.removeSuffix("/") }
  val h = urlStringLists.any { it?.startsWith("file://") ?: false }
  if (!h) mavenLocal()
}

beforeSettings {
  pluginManagement {
    repositories {
      addNonExistsMavenLocal()
      enableMirror()
      mavenCentral()
      google()
      gradlePluginPortal()
      otherRepos.forEach { addNonExistsRepo(it) }
      metaRepoMap.forEach { addNonExistsRepo(it.value, it.key) }
    }
  }
  dependencyResolutionManagement {
    repositories {
      addNonExistsMavenLocal()
      enableMirror()
      mavenCentral()
      google()
      gradlePluginPortal()
      otherRepos.forEach { addNonExistsRepo(it) }
      metaRepoMap.forEach { addNonExistsRepo(it.value, it.key) }
    }
  }
}

allprojects {
  buildscript {
    repositories {
      addNonExistsMavenLocal()
      enableMirror()
      mavenCentral()
      google()
      gradlePluginPortal()
      otherRepos.forEach { addNonExistsRepo(it) }
      metaRepoMap.forEach { addNonExistsRepo(it.value, it.key) }
    }
  }
  repositories {
    addNonExistsMavenLocal()
    enableMirror()
    mavenCentral()
    google()
    gradlePluginPortal()
    otherRepos.forEach { addNonExistsRepo(it) }
    metaRepoMap.forEach { addNonExistsRepo(it.value, it.key) }
  }
}
